(reimplementation of core utils CAT command in vox)

A text called "Program Version" is "0.1.8".

(Parse the flags now)
A flag called "wants help" is "--help" or "-h", it is a boolean.
A flag called "wants version" is "--version" or "-v", it is a boolean.
A flag called "we are numbering lines" is "--number" or "-n", it is a boolean.

To "show version".
  A text called "Program" is arguments's name.
  Print "{Program} (vox) {Program Version} by Josjuar Lister".
  Exit 0.

If "wants help" then,
  A text called "Program" is arguments's name,
  Print "{Program} - contatonate files and print on the standard output",
  Print "Usage: {Program} [OPTION]... [FILE]...",
  Print "With no FILE, or when FILE is -, read standard input.",
  Print "\nOptions:",
  Print "  -h, --help     display this help and exit",
  Print "  -n, --number   number all output lines",
  Print "  -v, --version  output version information and exit",
  Print "\nExamples:",
  Print "\t{Program} f - g\tOutput f's content, then standard input, then g's contents",
  Print "\t{Program}\t\tCopy standard input to standard output",
  Exit 0.

If "wants version" then,
  "show version".

(Prepare to cat)
a boolean called "failed to open a file" is false.

Open a file for writing called "output" at 1(fd - stdout).
On error print "cat: /dev/stdout: Could not open pipe", exit 1.

Create a buffer called "content".
On error print "cat: failed to create buffer", exit 1.

Create a buffer called "staged_output".
On error print "cat: failed to create buffer", exit 1.

If "we are numbering lines" then,
  A number called "Line Number" is 1.

If arguments's empty then,
  Open a file for reading called "source" at 0(fd - stdin),
  On error print "cat: /dev/stdin: Could not open pipe", exit 1.
  Read line from source into content,
  While content is not empty,
    if "we are numbering lines" then,
      Append "{Line Number:6}\t{content}" to staged_output,
      increment the "Line Number".
    Otherwise,
      Append content to staged_output.
    if staged_output's size is greater than 65535 then,
      Write staged_output to output,
      Clear staged_output.
    read line from the source into content.
if arguments's empty then,
  Write staged_output to output,
  Clear staged_output.

(Loop expansion: open a file for each filename in arguments's all)
Open a file called "source" for reading at each filename from arguments's all treating "-" as "/dev/stdin",
  On error print "cat: {filename}: No such file or directory",
    set "failed to open a file" to true,
    continue.
  read line from the source into content,
  while the content is not empty,
    if "we are numbering lines" then,
      append "{Line Number:6}\t{content}" to staged_output,
      increment the "Line Number".
    Otherwise,
      append content to staged_output.
    if staged_output's size is greater than 65535 then,
      Write staged_output to output,
      Clear staged_output.
    read line from the source into content.
  Close source,
  Write staged_output to output,
  Clear staged_output.
    
If "failed to open a file" then,
  Exit 1.
Otherwise,
  Exit 0.
  